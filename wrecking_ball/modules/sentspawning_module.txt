@name sentspawningmodule
@persist SENTS_tick:function SENTS_entityCreated:function SENTS_Turrets:array SENTS_CamController:entity SENTS_TurretCallback:function SENTS_CamControlCallback:function
@persist SENTS_ShouldHint SENTS_Manual
@persist SENTS_SETUP_TURRETS:string SENTS_SETUP_CAMCONTROLLER:string
@persist [SENTS_Null_EntityCreated, SENTS_Null_Tick, SENTS_MonitorManual, SENTS_Turrets1, SENTS_Turrets2, SENTS_Turrets3, SENTS_CamController1, SENTS_CamController2, SENTS_RunCallbacks]:function

if( first() ){
    
    SENTS_SETUP_TURRETS =                                 #
        "wire_turret_tracernum 1;" +                      #
        "wire_turret_tracer Tracer;" +                    #
        "wire_turret_model models/weapons/w_smg1.mdl;" +  #
        "wire_turret_sound Weapon_AR2.Single;" +          #
        "wire_turret_numbullets 1;" +                     #
        "wire_turret_force 1.0;" +                        #
        "wire_turret_damage 8;" +                         #
        "wire_turret_automatic 0;" +                      #
        "gmod_toolmode wire_turret"                       #
        
    SENTS_SETUP_CAMCONTROLLER =                                         #
        "wire_cam_allowzoom 0;" +                                       #
        "wire_cam_automove 1;" +                                        #
        "wire_cam_autounclip 1;" +                                      #
        "wire_cam_autounclip_ignorewater 0;" +                          #
        "wire_cam_drawparent 1;" +                                      #
        "wire_cam_drawplayer 0;" +                                      #
        "wire_cam_freemove 0;" +                                        #
        "wire_cam_localmove 0;" +                                       #
        "wire_cam_model models/jaanus/wiretool/wiretool_range.mdl;" +   #
        "wire_cam_parentlocal 1;" +                                     #
        "gmod_toolmode wire_cam"                                        #
    
    SENTS_Turrets = array()
    SENTS_ShouldHint = 1
    
    SENTS_Null_EntityCreated = function(_:entity){}
    SENTS_Null_Tick          = function(){}
    
    SENTS_entityCreated = SENTS_Null_EntityCreated
    SENTS_tick          = SENTS_Null_Tick
    
    function sents_uninitialized_warnings(){
        SENTS_CamController = noentity()
        SENTS_MonitorManual = SENTS_Turrets1 = SENTS_Turrets2 = SENTS_Turrets3 = SENTS_CamController1 = SENTS_CamController2 = SENTS_RunCallbacks = SENTS_Null_Tick
    }
    
    function sents_onCamControllerReady(Callback:function){
        SENTS_CamControlCallback = Callback
    }
    function sents_onTurretsReady(Callback:function){
        SENTS_TurretCallback = Callback
    }
    
    function sents_resetFetched(E:entity){
        E["sents_consumed",number] = 1
        E:constraintBreak()
        E:propFreeze(1)
        E:setPos(entity():pos()+vec(0,0,10))
    }
    
    function sents_startSpawning(){
        
        if( !concmd("-attack") ){
            print("> sentspawningmodule: concmd is disabled; can't auto-spawn wire components.")
            print("> sentspawningmodule: do \"wire_expression2_concmd 1\" in console and try again.")
            print("> sentspawningmodule: check modules/sentspawning_module.txt for details.")
            print("> sentspawningmodule: spawning them manually does work, but is not advised!")
            owner():soundPlay("sents_errorsound",1,"buttons/button18.wav")
            timer("sents_error2",1000)
            SENTS_entityCreated = SENTS_MonitorManual
            SENTS_Manual = 1
        }
        else{
            print("> sentspawningmodule: setting up...")
            SENTS_tick          = SENTS_Turrets1
            SENTS_entityCreated = SENTS_Null_EntityCreated
            SENTS_Manual = 0
            timer("sents_helphint",5000)
        }

    }
    
    SENTS_Turrets1 = function(){
        if( !owner():keyAttack1() & owner():weapon():type() == "gmod_tool" ){
            SENTS_tick          = SENTS_Turrets2
            SENTS_entityCreated = SENTS_Null_EntityCreated
            
            SENTS_ShouldHint = 0
            
            concmd(SENTS_SETUP_TURRETS)
        }
    }
    SENTS_Turrets2 = function(){
        if( owner():tool() == "wire_turret" & !owner():keyAttack1() & owner():aimEntity():type() != "gmod_wire_turret" ){
            SENTS_tick          = SENTS_Null_Tick
            SENTS_entityCreated = SENTS_Turrets3
            
            concmd("+attack")
        }
    }
    SENTS_Turrets3 = function(Ent:entity){
        
        if( 
            Ent:type() != "gmod_wire_turret" |
            Ent:owner() != owner() | 
            SENTS_Turrets:count() >= 4 |
            Ent["sents_consumed",number]
        ){ return }
        
        sents_resetFetched(Ent)
        SENTS_Turrets:pushEntity(Ent)
        if(!SENTS_Manual){ 
            if(SENTS_Turrets:count() < 4){
                SENTS_tick          = SENTS_Turrets2
                SENTS_entityCreated = SENTS_Null_EntityCreated
                
                concmd("-attack")
            }
            else{
                print("> sentspawningmodule: made turrets...")
                SENTS_tick          = SENTS_CamController1
                SENTS_entityCreated = SENTS_Null_EntityCreated           
                
                concmd("-attack;" + SENTS_SETUP_CAMCONTROLLER)
            }
        }
        else {
            if(SENTS_Turrets:count() == 4){
                SENTS_TurretCallback(SENTS_Turrets)
            }
        }
        
    }
    
    SENTS_CamController1 = function(){
        if( owner():tool() == "wire_cam" & !owner():keyAttack1() ){
            SENTS_tick          = SENTS_Null_Tick
            SENTS_entityCreated = SENTS_CamController2
            
            concmd("+attack")
        }        
    }
    SENTS_CamController2 = function(Ent:entity){
        
        if(
            Ent:type() != "gmod_wire_cameracontroller" |
            Ent:owner() != owner() | 
            SENTS_CamController:isValid() |
            Ent["sents_consumed",number]
        ){ return }
        
        sents_resetFetched(Ent)
        SENTS_CamController = Ent
        if(!SENTS_Manual){ 
            SENTS_tick          = SENTS_RunCallbacks
            SENTS_entityCreated = SENTS_Null_EntityCreated       
            
            print("> sentspawningmodule: made cam controller...")
                
            concmd("-attack; gmod_toolmode wire_expression2") 
        }
        else {
            SENTS_CamControlCallback(SENTS_CamController)
        } 
    }
    
    SENTS_MonitorManual = function(E:entity){
        SENTS_Turrets3(E)
        SENTS_CamController2(E)
    }
    
    SENTS_RunCallbacks = function(){
        print("> sentspawningmodule: done!")
        concmd("wire_turret_automatic 1")
        SENTS_TurretCallback(SENTS_Turrets)
        SENTS_CamControlCallback(SENTS_CamController)
        SENTS_tick = SENTS_Null_Tick
    }
    
}


event tick(){
    SENTS_tick()
}

event entityCreated(Ent:entity) {
    SENTS_entityCreated(Ent)  
}

if(clk("sents_helphint") & SENTS_ShouldHint){
    SENTS_ShouldHint = 0
    print("> sentspawningmodule: this is taking a while... make sure your toolgun is out")
}

