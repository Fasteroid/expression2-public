@name beam of stupidity
@persist NUM_OF_PARTICLES Particles:array LastParticle:entity Beam_State BEAM_SOUNDS:array

SpawnedCount = Particles:count()

if( first() ){
    
    print("--= beam of stupidity =--")
    print("       - by Fasteroid -")
    print(" ")
    print("     - INSTRUCTIONS -")
    print("  1. Equip Gravity Gun")
    print("  2. Hold right mouse ;)")
    
    NUM_OF_PARTICLES = 32
    BEAM_SOUNDS = array(
        "Town.d1_town_04_spin_loop",
        "Phx.Alien1",
        "Phx.Alien1",
        "Phx.Alien1"
    )
    
    enableConstraintUndo(0)
    propSpawnUndo(0)
    rangerPersist(1)
    runOnTick(1)
    rangerHitEntities(0)
    
    function stopbeam(){
        stoptimer("evillaugh")
        if(Beam_State){
            foreach(N,K:entity = Particles){
                T = (N-1)/(SpawnedCount-1)
                soundVolume("stupid"+N,0)
                soundVolume("idiots"+N,0)
                K:propFreeze(1)
                K:setPos( entity():pos()+vec(0,0,N*0.1) )
                K:removeTrails()
            }
            Beam_State = 0
        }
    }
    
    function startbeam(BeamStart:vector, BeamEnd:vector){
        Pitch = randint(50,70)
        Beam_State = 1
        Particles[NUM_OF_PARTICLES,entity]:propFreeze(1)
        Particles[1,entity]:propFreeze(1)
        foreach(N,K:entity = Particles){
            soundVolume("stupid"+N,100)
            soundVolume("idiots"+N,100)
            T = (N-1)/(SpawnedCount-1)
            if(N>1 & N<SpawnedCount){
                K:propFreeze(0)
                K:setPos( mix(BeamEnd,BeamStart,T) )
                K:setTrails(150,0,0.5,"trails/laser",vec(100*(N%2),120*((N+1)%2),255),255)
            }
        }
        timer("evillaugh",random(4000,6000))
    }
    
}

    
while( SpawnedCount < NUM_OF_PARTICLES & propCanCreate() ){

    NextParticle = propSpawn("models/expression 2/cpu_microchip_nano.mdl",entity():pos()+vec(0,0,SpawnedCount*0.01),1)
    
    if( NextParticle:isValid() ){
    
        holoCreate(SpawnedCount,NextParticle:pos())
        holoModel(SpawnedCount,"models/buildables/breadmonster_sapper_placed.mdl")
        holoScale(SpawnedCount,vec())
        holoAlpha(SpawnedCount,0)
        holoParent(SpawnedCount,NextParticle)
        
        NextParticle:setMass(50000)
        NextParticle:setAlpha(0)
        
        local Sound = BEAM_SOUNDS[(SpawnedCount % BEAM_SOUNDS:count()) + 1, string]
        
        NextParticle:soundPlay("stupid"+(SpawnedCount+1),0,"synth/saw_inverted_440.wav")
        holoEntity(SpawnedCount):soundPlay("idiots"+(SpawnedCount+1),0,Sound)
        soundPitch("stupid"+(SpawnedCount+1),15)
        soundPitch("idiots"+(SpawnedCount+1),140)
            
        Particles:pushEntity(NextParticle)
        if(LastParticle:isValid()){
            rope(SpawnedCount,LastParticle,vec(),NextParticle,vec(),0,10,"lights/white",1)
        }
        LastParticle = NextParticle
        rangerFilter(NextParticle)
        
        SpawnedCount = Particles:count()
    
    }
    else {
        break
    }
}

if( owner():isAlive() & owner():weapon():type() == "weapon_physcannon" & SpawnedCount >= NUM_OF_PARTICLES ){
    
    BeamEnd   = rangerOffset(99999,owner():shootPos(),owner():eye()):pos()
    BeamStart = owner():shootPos() + owner():eye()*80 + vec(0,-30,-30):rotate(owner():eyeAngles()) + owner():vel()/7
    
    if( owner():keyAttack2() ){
        Particles[SpawnedCount,entity]:setPos( BeamEnd )
        Particles[1,entity]:setPos( BeamStart )
        foreach(N,K:entity = Particles){
            K:applyForce(randvec()*5000000)
        }
    }
    
    if( changed( owner():keyAttack2() ) ){
        
        if( owner():keyAttack2() ){
            startbeam(BeamStart, BeamEnd)
        }
        else{
            stopbeam()
        }
        
    }

}
else{
    stopbeam()
}
    
